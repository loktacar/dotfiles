# vim: set fdm=marker :
#
# @author: Viktor Alex Brynjarsson
#
# My .zshrc
#

# {{{ modules, options and settings
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
PATH=$PATH:~/bin
PAGER=less

autoload -U colors && colors
autoload -Uz compinit && compinit

zmodload zsh/mathfunc
zmodload zsh/files

setopt APPEND_HISTORY
setopt AUTO_CD
setopt NOTIFY
setopt CORRECTALL
setopt GLOBDOTS
setopt INC_APPEND_HISTORY
setopt AUTO_MENU
setopt REC_EXACT
setopt EXTENDED_GLOB
setopt AUTO_PARAM_SLASH

unsetopt BEEP
unsetopt NO_MATCH

zstyle :compinstall filename '/home/viktor/.zshrc'
# }}}

# {{{ key bindings
# use a vim style input
bindkey -v

# search backwards and forwards using default vim keys (in normal mode)
bindkey -M vicmd '/' history-incremental-pattern-search-backward
bindkey -M vicmd '?' history-incremental-pattern-search-forward
# if I'm not in normal mode Ctrl+r or Ctrl+s will search backwards and
# forwards respectively
bindkey -M viins '^R' history-incremental-pattern-search-backward
bindkey -M viins '^S' history-incremental-pattern-search-forward
# same as normal mode if I've already started searching
#bindkey -M isearch '^R' history-incremental-pattern-search-backward
#bindkey -M isearch '^S' history-incremental-pattern-search-forward
# }}}

# {{{ completion
setopt COMPLETE_IN_WORD

# try to fix errors too
zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:match:*' original only
zstyle -e ':completion:*:approximate:*' \
        max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3))numeric)'

# menu plox
zstyle ':completion:*:*:*:*:*' menu yes select

# kill command completion
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*'   force-list always
# }}}

# {{{ prompt

# left prompt examples:
#   ~/dotfiles master %
#   ~ %
# right prompt examples:
#   viktor@einstein --INS--
#   viktor@einstein --NOM--

branch_name=" "
vim_mode="--INS--"

function zle-keymap-select zle-keymap-init {
  vim_mode="${${KEYMAP/vicmd/--NOM--}/(main|viins)/--INS--}"
  RPROMPT="%{$fg[green]%}%n%{$reset_color%}@%{$fg[blue]%}%m%{$reset_color%} %B${vim_mode}%b"
  zle reset-prompt
}
zle -N zle-keymap-select
zle -N zle-keymap-init

precmd() {
  branch_name="$(git symbolic-ref -q HEAD 2>/dev/null)"

  if [[ $branch_name != '' ]]; then
    branch_name=" ${branch_name##refs/heads/} "
  else
    branch_name=" "
  fi

  PROMPT="%{$fg[yellow]%}%~%{$fg[red]%}${branch_name}%{$reset_color%}%# "
}

RPROMPT="%{$fg[green]%}%n%{$reset_color%}@%{$fg[blue]%}%m%{$reset_color%} %B${vim_mode}%b"
# }}}

# {{{ aliases
# default flags and 'settings'
alias ls='pwd && ls --color -hF'
alias ll='ls -l'
alias la='ls -a'
alias lla='ls -al'
alias lsa='ls -a'
alias less='less -r'
alias df='df -h'

# oops
alias sl='ls'
alias dc='cd'
alias cd..='cd ..'

# if I'm stuck in vim mode
alias :q='exit'

# aliases pertaining to completion
alias man='nocorrect man'
alias mv='nocorrect mv'

# uptime gives me too little info, here a fix
# example:
# Linux loktacar-laptop 2.6.35-22-generic #35-Ubuntu SMP Sat Oct 16 20:36:48 UTC 2010 i686 GNU/Linux
#  Thu Mar 24 00:34:06 GMT 2011
#  00:34:06 up 4 days,  5:12,  2 users,  load average: 0.25, 0.42, 0.53
#
#              total       used       free     shared    buffers     cached
# Mem:          1974       1896         77          0         59       1178
# -/+ buffers/cache:        658       1315
# Swap:         7627          0       7627
alias uptime='uname -a && echo " "`date`"" && uptime && echo && free -m'

# whe? wha? who?
alias whe='whereis'
alias wha='whatis'

# global aliases, can be used anywhere in a line
alias -g ...="../.."
alias -g ....="../../.."
alias -g .....="../../../.."
alias -g L="| less"
alias -g G="| grep"
alias -g H="| head"
alias -g T="| tail"
# }}}
